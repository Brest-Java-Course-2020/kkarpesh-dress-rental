plugins {
  id "com.github.spotbugs" version "4.0.8"
}

allprojects {
    group = 'com.epam.brest.courses'
    version = '1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }

    sourceCompatibility = '11'

    test {
       useJUnitPlatform()
       failFast = true
       finalizedBy jacocoTestReport
    }

    checkstyle {
        toolVersion = '8.2'
        showViolations = true
        ignoreFailures = false
        checkstyleTest.enabled = false
    }

    spotbugs {
        effort = 'min'
        reportLevel = 'low'
        excludeFilter = rootProject.file('config/spotbugs/spotbugs-exclude.xml')
    }

    jacoco {
        toolVersion = "0.8.5"
    }

    jacocoTestReport {
        dependsOn test
    }

    test.mustRunAfter checkstyleMain, checkstyleTest

}

def exportedProjects = [
       ':dress-rental-dao',
       ':dress-rental-model',
       ':dress-rental-rest-app',
       ':dress-rental-service',
       ':dress-rental-service-api',
       ':dress-rental-service-rest',
       ':dress-rental-test-db',
       ':dress-rental-web-app'
]

task alljavadoc(type: Javadoc) {
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/docs/javadoc")
}



