plugins {
  id 'org.springframework.boot' version '2.3.0.RELEASE'
  id "com.github.spotbugs" version "4.0.8"
  id "io.spring.dependency-management" version "1.0.9.RELEASE"
}

allprojects {
    group = 'com.epam.brest.courses'
    version = '1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    repositories {
        mavenCentral()
    }

    sourceCompatibility = '11'

    dependencyManagement {
        imports {
          mavenBom("org.springframework.boot:spring-boot-dependencies:2.3.0.RELEASE")
        }
      }


    test {
       useJUnitPlatform()
       failFast = true
       finalizedBy jacocoTestReport
    }

    checkstyle {
        toolVersion = '8.2'
        showViolations = true
        ignoreFailures = false
        checkstyleTest.enabled = false
    }

    spotbugs {
        effort = 'min'
        reportLevel = 'low'
        excludeFilter = rootProject.file('config/spotbugs/spotbugs-exclude.xml')
    }

    jacoco {
        toolVersion = "0.8.5"
    }

    jacocoTestReport {
        dependsOn test
    }

    test.mustRunAfter checkstyleMain, checkstyleTest

}

def exportedProjects = [
       ':dress-rental-dao',
       ':dress-rental-model',
       ':dress-rental-rest-app',
       ':dress-rental-service',
       ':dress-rental-service-api',
       ':dress-rental-service-rest',
       ':dress-rental-test-db',
       ':dress-rental-web-app'
]

task alljavadoc(type: Javadoc) {
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/docs/javadoc")
}



